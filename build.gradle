buildscript {
    ext.kotlin_version = '1.5.30'

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'

        classpath 'com.google.firebase:firebase-appdistribution-gradle:2.1.3'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.7.1'

        classpath 'com.google.gms:google-services:4.3.10'

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

/**
 * Shared configs across subprojects
 */
ext.projectArtifactId = { project ->
    if (project.name == 'core' || project.name == 'coil') {
        return project.name
    } else {
        return null
    }
}

allprojects {
    group = 'com.github.zhombie.kenes-widget-android'
    version = '4.0.0-alpha01'
    project.ext.artifactId = rootProject.ext.projectArtifactId(project)

    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

/**
 * Publishing and signing info
 */
subprojects { project ->
    if (project.ext.artifactId == null) return

    apply plugin: 'com.android.library'
    apply plugin: 'maven-publish'
    apply plugin: 'kotlin-android'

    publishing {
        publications {
            aar(MavenPublication) {
                groupId project.group
                artifactId project.ext.artifactId
                version project.version

                pom {
                    name = project.name
                    url = "https://github.com/zhombie/kenes-widget-android"

                    developers {
                        developer {
                            name = 'zhombie'
                        }
                    }
                }

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    project.configurations.api.allDependencies.each { dependency ->
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }

                afterEvaluate {
                    artifact "$buildDir/outputs/aar/$project.name-release.aar"
                }
            }
        }
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}
